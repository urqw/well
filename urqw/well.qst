:new
pln "Колодец"
pln версия 1.5
pln по мотивам рассказа "Колодец"
pln  
pln   
pln Автор Lord Demon
pln demongamer@narod.ru
pln
pln программист - незабвенный RipOs
pln http://ripsoft.narod.ru/Ripsoft.htm
pln   
pln   
pln Особое спасибо Victor'у за тестирование и моральную помощь.
pln 
pln Сайт Кащея http://www.textquest.boom.ru/
btn 1,Начать
end

:1
dp=2
жизней=90
сытость=80
сил=90
inv+ 90,Жизней
inv+ 80,Сытость
inv+ 90,Сил
tr=5
pln  "Людям жить лучше всего, когда у каждого есть свое определенное место, когда каждый знает, в какой задуманный порядок он вписан и чего может достичь. Разрушь отведенное место - и ты разрушишь личность..."
pln              "Дюна"
pln
pln
pln
pln
pln Голова адски раскалывалась. Да и было от чего... после удара рукояткой меча обязательно останется шишка, хотя может и не останется... как же она останется, если я сейчас умру?
pln Взгляд вниз открыл провал в полтора десятка метров с бурлящей водой. Лишь небольшие перильца слева отделяли мужчину от пропасти. Справа возвышалась каменная стена, спереди, помост с которого сбрасывали таких как он...
pln Не даром это место называют колодцем. Сюда падает вода с одной из речушек и сразу же, превращаясь в водоворот, уходит ввниз, под землю. И словно в насмешку, природа создала здесь, в яме в сотню метров шириной, островок вместе со скалой высотой в десяток метров, где умирают от голода немногие выжившие. Интересно, а что делали с трупами? Спускались и скидывали в воду? Хотя нет, лучше кинуться в воду, чем голодать тут, постепенно умирая... Страшное место... А эти садисты ещё и подвесили над самой верхушкой скалы веревку. Ему уже сказали, что если взобраться по ней до конца, тогда тебя освободят от всех обвинений, отпустят...  Но это только мечта. Можно ли взобраться обессиленному человеку по такому канату в 25 метров? 
pln В спину больно укололо острие копья и грубый голос произнес: "Пошел, чернь! Или тебя отсюда скинуть?!"
btn 1a1,Наклон и атака на стражников
btn 1a2,Стоять
btn 1a3,Спрыгнуть
end

:1a1
pln Вы быстро ушли вниз и, в развороте, бросились на стражников снизу. А зря, охрана знала свое дело, и вы просто наткнулись на копье расположенное снизу...
end

:1a2
pln А зачем собственно прыгать? Не лучше ли так умереть? Но умереть вам не дали. Удар и вы летите вниз....
жизней=жизней-5
inv- 5,Жизней
goto 1b1
end

:1a3
pln Быстрый разбег и вы летите вниз. 
goto 1b1
end

:1b1
pln Вы падаете ровно в бурлящий водоворот, более похожий на пасть подводного чудовища.
btn 1b2,Вдохнуть
btn 1b3,Закрыть глаза
end

:1b3
pln Пучина мягко приняла вас
end

:1b2
жизней=жизней-20
сил=сил-20
inv- 20,Жизней
inv- 20,Сил
pln Вдохнув воздуха и вытянувшись стрункой, вы канули в глубину. 
btn 1c1,Вынырнуть
btn 1c2,Плыть глубже
end

:1c1
pln Захлёбываясь вы вынырнули на поверхность. Но пена, брызги, волны не дают нормально дышать. А вода кружится и кружится, уходя на глубину вместе с вами...
end

:1c2
pln Кислорода ещё хватает и вы плывете все глубже и глубже, прямо в тонкий хобот водоворота. Уши уже давно заложило, а по легким прошелся спазм.
inv- 15,Жизней
inv- 20,Сил
жизней=жизней-15
сил=сил-20
btn 1c3,Плыть вбок
btn 1c1,Верх!
end

:1c3
жизней=жизней-15
сил=сил-20
inv- 15,Жизней
inv- 20,Сил
pln Вы вырвались из плена и плывете дальше, в сторону от водоворота. Легкие уже разрываются
btn 1c4,Дальше
btn 1c1,Вверх
end

:1c4
pln В глазах уже давно туман, но руки продолжают монотонно грести. Цель - выжить, вот что главное! Но тело берет свое и вы, готовясь умереть, рветесь наверх, к свету, к воздуху и будь что будет.
pln Воздух! Спасительный воздух... как болит голова...
btn 2a1,Все..
end

:2a1
ракушка=7
inv- 30,Жизней
inv- 20,Сил
жизней=жизней-30
сил=сил-20
pln И снова голова разваливается на части... Вы лежите на каменистом пляже, на том самом островке, где вам суждено умереть. 
pln Единственная радость-тут есть моллюски, так что сразу не помрешь. 
btn 2a2,Все осмотреть
btn 2a3,Есть ракушки
btn 2a4,Отдыхать
btn 2a5,Взбираться вверх
btn 2z6,Зайти в воду
end

:2z6
pln Бр... как холодно... 
if rt=1 then goto 4s4b
btn 2b1,Выйти
end

:2a4
pln Вы устраиваетесь на камнях. Тут, конечно, не мягко, но отдохнуть вполне можно.
btn 2b1,Встать
btn 2a4a,Спать
end


:2a2
pln Каменное побережье и скала посередине в высоту около 10 метров и веревка над самой её вершиной. Если судить по обещаниям стражи, то если подняться по ней вооооон до той площадки, около 20 метров над вами, то тебя отпустят, вернее ты сам сможешь уйти, хотя ведь веревка продолжается ещё на десяток-другой метров... интересно, а что там, ещё выше?
pln Но это мечты... В этом каменном мешке всегда холодно, даже палящее солнце не может согреть камни, омываемые холодной водой. Представляю каково тут ночью...
goto 2b1
end

:2a3
if ракушка<1 then goto 2a3a
сил=сил-1
inv- 1,Сил
ракушка=ракушка-1
сытость=сытость+3
inv+ 3,Сытость
pln Вы отскребаете мякоть и жадно пожираете её. Что делать, другого нет...
if скорлупа=0 then inv+ Ракушка
скорлупа=скорлупа+1
if rt=1 then goto 4s4b
goto 2b1
end









:2b1
if жизней<1 then goto die
if сил<1 then pln Вы слишком устали и, упав на камни, заснули...
if сил<1 then goto 2a4a
if мужик=1 then btn 3a2,Подойти к Хенву
btn 2a2,Все осмотреть
btn 2a3,Есть ракушки
if скорлупа>0 then btn 2a3b,Заточить скорлупу
btn 2a4,Отдыхать
btn 2a5,Взбираться вверх
btn 2z6,Зайти в воду
btn sport,Заняться физкультурой
end

:die
pln Отсутствие пищи приводит к плачевным результатам...
end

:sport
if сытость < 15 then pln Вам бы поесть сначала...
if сытость < 15 then goto 2b1
pln Занятие физкультурой на камнях не самое благодатное дело. О да, мышцы окрепли, но вам это даром не обошлось...
inv- 5,Жизней
жизней=жизней-5
inv+ 20,Сил
сил=сил+20
сытость=сытость-20
Inv- 20,Сытость
if rt=1 then goto 4s4b
goto 2b1
end

















:2a4a
день=день+1
ракушка=RND*10
pln Наконец ваши кошмары кончились и вы можете встать с каменного ложа. Сил прибавилось, а желудок болит...

if сытость<20 then inv- 10,Жизней
if сытость<20 then inv- 10,Сил
if сытость<20 then жизней=жизней-10
if сытость<20 then сил=сил-10
if сытость>20 then жизней=жизней + 10
if сытость>20 then Inv+ 10,Жизней
if сытость>20 then сил=сил + 15
if сытость>20 then inv+ 15,Сил
if сил<20 then inv- 10,Жизней
if сил<20 then жизней=жизней-10
if жизней<20 then сил=сил-10
if жизней<20 then inv- 10,Сил
сытость=сытость-20
inv- 20,Сытость
if сил>70 then сил=сил-15 & inv- 15,Сил
if жизней>100 then жизней=жизней-15 & inv- 15,Жизней
if сытость < 1 then goto die

if день=8 then goto 3a1
if день=3 then goto 2a4q
if жизней<1 then goto die
goto 2b1
end


:2a4q
pln Крик сверху привлекает ваше внимание и вы видите, как в воду сбрасывают ещё одного человека. С криком он летит вниз и падает в водоворот. Как вы не вглядывались, больше вы его уже не увидели...
goto 2b1
end



:3a1
pln Вы снова просыпаетесь от крика. На этот раз не страха, а боли. Перед вами развернулась красочная картина. Человек на площадке ударил стражника по лицу камнем и, укорачиваясь от копья, сорвался вниз. Но он не растерялся и в полете, развернувшись, нырнул в воду, словно рыба. Минуты через пол он уже появился на поверхности и начал активно грести руками, надеясь выбраться из потока. Сил у него было много и только это, видно, спасло его. Уже обессиленный, так же как когда-то и вы, он оказался на берегу
мужик=1
goto 2b1
end



:2a3a
pln К вашему ужасу все ракушки кончились. Теперь придется ждать пока появятся новые...
if rt=1 then goto 4s4b
goto 2b1
end



:3a2
pln Смуглый жилистый парень, лежащий на камнях. Сразу видно, что он человек "сильной руки", привыкшей к тяжелой работе. На спине татуировка в виде медведя.
btn 3a3,Оттащить от воды
btn 2b1,Оставить
end

:3a3
pln Вы сразу оттаскиваете парня от воды. Чуть полежав на неудобных камнях, он открывает глаза, поворачивает голову и что-то шепчет
btn 3a4,Наклониться
btn 3a5,Дать моллюска
btn 3a6,Ждать
end

:3a4
pln Нет, не слышно...
btn 3a5,Дать моллюска
btn 3a6,Ждать
end
 
:3a5
pln Слабая рука берет моллюска, подносит к глазам и откидывает в сторону. И действительно, зачем он ему сейчас? Перед выходом всех кормят...
btn 3a6,Ждать
end

:3a6
pln Минут через 10 он полностью приходит в себя и садится. По его словам, его зовут Хенв и его сбросили сюда за убийство, но он хочет выбраться отсюда и думает что только со взаимной помощью можно выбраться отсюда. Вы немного удивляетесь - этот остров не может прокормить обоих и, судя по всему, Хенв отлично знает об этом.
pln Тут снова слышно громыхание метала и видно, как стражи ведут ещё одного заключенного. Он отчаянно защищается, но его скидывают вниз, в водоворот. И снова начинается борьба со смертью. И снова человек выбирается из водоворота и плывет к острову. Немного посмотрев на него, Хенв поднимается и идет в воду (откуда столько силы?), он плывет навстречу утопающему и помогает выбраться ему на берег. Тот падает лицом вниз и пытается отдышаться, а Хенв подходит сзади и мощным ударом ноги по шее переламывает хребет. А потом он оборачивается к вам и улыбается:
pln -Теперь есть пища!
btn 4a1,Ждать
btn 4a2,Спать
end

:4a2
день=день+1
ракушка=RND*10 + 2
pln Наконец ваши кошмары кончились и вы можете встать с каменного ложа. Сил прибавилось, а желудок болит...


if сытость<20 then inv- 10,Жизней
if сытость<20 then inv- 10,Сил
if сытость<20 then жизней=жизней-10
if сытость<20 then сил=сил-10
if сытость>20 then жизней=жизней + 10
if сытость>20 then Inv+ 10,Жизней
if сытость>20 then сил=сил + 15
if сытость>20 then inv+ 15,Сил
if сил<20 then inv- 10,Жизней
if сил<20 then жизней=жизней-10
if жизней<20 then сил=сил-10
if жизней<20 then inv- 10,Сил
сытость=сытость-20
inv- 20,Сытость
if сил>70 then сил=сил-15 & inv- 15,Сил
if жизней>100 then жизней=жизней-15 & inv- 15,Жизней
if сытость < 1 then goto die

if жизней<1 then goto die
goto 4a1
end















:4a1
pln Хенв уже успел обойти весь остров и досконально обследовать его. Теперь он заявил:
pln -Нам надо будет набраться сил с помощью этого - показал пальцем на труп - а через неделю можно будет лезть по веревке.
pln Тут же, не дожидаясь ответа, он подсел к трупу и ногтями стал отрывать кусок бедра. Вас, при открывшейся перспективе, вырвало бы, если бы было чем.
btn 4a1a,Начать есть
btn 4a1b,Сесть в углу
end

:4a1a
pln Вы тоже отодрали кусок человечины и принялись есть. Хенв лишь улыбнулся и подмигнул: "А ты умнее, чем я думал"
btn 4b1a,Есть
btn 4a1b,Отбросить
end

:4a1b
pln -Ну и дурак, зло бросил Хенв и впился зубами в мясо.
btn 4a1a,Начать есть
btn 4a1c,Искать моллюсков
end

:4a1c
pln Вы прошлись по островку и набрали только двух моллюсков
сытость=сытость+4
Inv+ 4,Сытость
goto 4a1v
end

:4a1v
we=1
btn 4a1a,Начать есть
btn 2a5,Взбираться
btn 4s1,Спать
end

:4b1a
pln Вы с отвращением впились в мясо, почувствовав кровь во рту. Похоже сегодня ночью вам придется вдоволь помучиться. 
сытость=сытость+30
Inv+ 30,Сытость
btn 2a5,Взбираться
btn 4s1,Спать
end

:4s1
pln Вы уже легли спать, но живот протестующе ворчал. Похоже вы отравились... Одно радовало - Хенву было не лучше. Шатаясь, он пошел к воде. Тут к вам начали закрадываться подлые мысли: а зачем я ему нужен? Я только съедаю то, что мог съесть он. Может быть на время когда припасов может не хватить? Тогда я только кусок мяса, склад для этого извращенца! 
pln Месть поднималась внутри тугим комком и заполняло все нутро. Как раз выдался хороший момент чтобы отомстить - он стоит спиной, наклонившись, по грудь в воде!
btn 4s1a,Забыть все и заснуть
btn 4s1b,Кинуться на него
if ножек=1 then btn 4s1c,Кинуться и ударить ножом
end


:4s1a
pln Странно, но на следующее утро вы не проснулись...
end


:4s1b
pln Вы бросайтесь ему на спину и опрокидываете Хенва в воду. 
jk=RND*10
if jk>4 then goto nm
goto nb
end

:nm
сил=сил-20
inv- 20,Сил
мужик=1
pln Он отчаянно защищается и почти скидывает вас с себя, но вы все же удерживаете его под водой еще немного... его попытки защищаться становятся все слабее и слабее... теперь он даже не дергается...
btn 4s4a,Подержать ещё
btn 4s4a,Вытащить из воды
end

:nb
pln Вроде бы он больше не сопротивляется, но когда вы чуть ослабили хватку, мощным ударом он сбрасывает вас с себя. Через секунду вы оказываетесь под ним, и ваш последний вопль тонет в воде.
end

:4s1c
мужик=1
pln Не задумываясь, вы прыгаете ему на шею и с силой проводите заточенной ракушкой по шее. Хенв падает в воду, но пытается вырваться... однако ж кровавая вода дает понять, что все кончено...
btn 4s4a,Подержать ещё
btn 4s4a,Вытащить из воды
сил=сил-10
inv- 10,Сил
end


:4s4a
rt=1
мужик=1
pln Через минуту вы ели-ели вытаскиваете его страшно-тяжёлое тело на берег. Нда... теперь во всяком случае вы можете спокойно поспать... и все бы хорошо, но теперь, когда злость ушла, в голову опять закрадываются плохие мысли: а вдруг он не хотел вас убивать, вдруг он действительно хотел взаимопомощи?
goto 4s4b
end

:4s4b
nrt=1
if жизней<1 then goto die
if сил<1 then pln Вы слишком устали и, упав на камни, заснули...
if сил<1 then goto 4s4c
if мужик=1 then btn 4s4m,Подойти к трупу
if мужик=2 then btn 4s4m,Подойти к трупу
if мужик=3 then btn 4s4m,Подойти к трупу
if мужик=4 then btn 4s4m,Подойти к трупу
if мужик=5 then btn 4s4mk,Подойти к трупу
btn 2a3,Есть ракушки
if скорлупа>0 then btn 2a3b,Заточить скорлупу
btn 4s4c,Отдыхать
btn 2a5,Взбираться вверх
btn 2z8,Зайти в воду
btn sport,Заняться физкультурой
end

:4s4mk
pln Странно... но человек не дышит...
мужик=1
goto 4s4b
end








:4s4c
if dp=0 then day=day-2
gh=1
day=day+1


if сытость<20 then inv- 10,Жизней
if сытость<20 then inv- 10,Сил
if сытость<20 then жизней=жизней-10
if сытость<20 then сил=сил-10
if сытость>20 then жизней=жизней + 10
if сытость>20 then Inv+ 10,Жизней
if сытость>20 then сил=сил + 15
if сытость>20 then inv+ 15,Сил
if сил<20 then inv- 10,Жизней
if сил<20 then жизней=жизней-10
if жизней<20 then сил=сил-10
if жизней<20 then inv- 10,Сил
сытость=сытость-20
inv- 20,Сытость
if сил>70 then сил=сил-15 & inv- 15,Сил
if жизней>100 then жизней=жизней-15 & inv- 15,Жизней
if сытость < 1 then goto die

день=день+1
ракушка=RND*10
if day=6 then ракушка=ракушка-4
if day=7 then ракушка=ракушка-4
if day>7 then ракушка=ракушка-2
pln Наконец ваши кошмары кончились, и вы можете встать с каменного ложа. Сил прибавилось, а желудок болит...
if жизней<1 then goto die
if day=7 then goto 4s5b
if day=8 then goto 4s4n
if day=8 then dp=0
goto 4s4b
end


:4s4m
if gh=0 then pln Сегодня вы уже ничего не съедите...
if gh=0 then goto 4s4b
gh=0
tr=tr-1
if tr<1 then goto 4s4k
pln Вы подходите к трупу. Нда... он может храниться здесь долго. Во всяком случае с голоду теперь не умрешь.
btn 4s4l,Есть
btn 4s4b,Отойти
end

:4s4l
pln Вы отрываете от туши куски плоти и с жадностью поглощаете её, с каждым разом все более убеждаясь, что каннибальство не так отвратительно, как видеться в первый раз.
сытость=сытость+30
inv+ 30,Сытость
goto 4s4b
end


:4s4k
pln Как ни печально, но есть тут больше нечего...
мужик=мужик-1
goto 4s4b
end


:4s5b
dp=1
pln И снова вы видите как стражники ведут заключенного на смерть. И почему-то опять мужчину, хотя в темнице сидят и женщины... интересно, почему? К тому же есть и интересная закономерность - людей сбрасывают только по утрам.
pln К этому времени человека, уже кувыркаясь, падал вниз. Маленькое тщедушное тельце кануло в пучину...
goto 4s4b
end

:4s4n
pln И снова звук сверху говорит о готовящемся убийстве. Бородатого человека в лохмотьях ведут к помосту. Он сам прыгает вниз и аккуратно, без брызг входит в воду. Вот он выныривает, но слишком близко от воронки и вода начинает засасывать его. Однако человек сопротивляется и все еще держится на расстоянии от неминуемой смерти. Но неминуемой ли?
dp=1
goto 4s4b
end

:2z8
pln Холодно...
if dp=1 then btn 2z9,Плыть к утопающему
goto 4s4b
end

:2z9
do=0
dp=0
pln Вы плывете к мужчине, чувствуя как поток постепенно захватывает вас... 
btn 2z10,Пыть дальше
btn 4s4b,Обратно
end

:down
pln Вы утонули...
end

:2z10
сил=сил-5
inv- 5,Сил
if сил<10 then goto down
pln Вы все плывете и плывете вперед, к уже еле гребущему в вашу сторону человеку...
btn 2z11,Вперед
btn 2z10a,Обратно
end

:2z11
tr=5
сил=сил-5
inv- 5,Сил
if сил<10 then goto down
pln Вы подплываете к человеку и...
btn 2z12,Топите
btn 2z13,Помогаете
end

:2z12
pln Вы прерываете отчаянное сопротивление и погружаете мужчину под воду...
мужик=1
btn 2z10a,Плыть обратно
end

:2z10a
сил=сил-5
inv- 5,Сил
if сил<10 then goto down
pln Усталый, но довольный, вы возвращаетесь...
btn 4s4b,Вперед
end

:2z13
pln Мужчина отчаянно цепляется за вас, мешая плыть.
сил=сил-10
inv- 10,Сил
if сил<10 then goto down
btn 2z14,Утопить
btn 2z15,Плыть к берегу
end

:2z15
dp=5
pln Мужчина левой рукой виснет на вас, мешая плыть и ели - ели гребет правой рукой...
сил=сил-10
inv- 10,Сил
if сил<10 then goto down
btn 2z14,Утопить
btn 2z15a,Плыть к берегу
end

:2z15a
if dp=5 then мужик=5
pln Вы уже недалеко от берега...
сил=сил-10
inv- 10,Сил
if сил<10 then goto down
btn 4s4b,Плыть к берегу
end


:2z14
if Вы прерываете отчаянное сопротивление и погружаете мужчину под воду... Через несколько секунд он перестает трепыхаться.
мужик=1
сил=сил-5
inv- 5,Сил
if сил<10 then goto down
btn 2z16,Плыть к берегу
end

:2z16
dp=1
pln Теперь он вас почти не стесняет и вы уже более свободно гребете руками
мужик=1
сил=сил-5
inv- 5,Сил
if сил<10 then goto down
btn 2z17,Плыть к берегу
end

:2z17
dp=1
pln Вы уже недалеко от берега...
сил=сил-5
inv- 5,Сил
if сил<10 then goto down
btn 4s4b,Плыть к берегу
end








:2a3b
if Ножек=1 then pln Вам вполне хватило мучений, связанный с первым осколком
if Ножек=1 then goto 2b1
inv- Ракушка
скорлупа=скорлупа-1
pln Вы долго затачиваете скорлупу от ракушки об камень. Сейчас получился довольно острый осколок. 
inv- 10,Сил
сил=сил-10
if rt=0 then btn 2a3c,Точить дальше
if rt=0 then btn 2a3d,Всё!
end

:2a3c
pln "Хрм!" сказал сломанный осколок
goto 2b1
end

:2a3d
pln Вы осматриваете ваш результат. Да... таким можно вспороть шею стражнику...
pln Вас внезапно привлек крик - оказывается за это время кто-то уже был скинут вниз, к вам. Тело упало в воду. Через секунду появилась голова, заработали руки и человек начал выбираться из водоворота. Казалось он уже выбрался, но вот случайно попал в стремнину и его затянуло опять, к центру. Сразу стало ясно что он уже без сил и на этот раз ему не выбраться. Поэтому вы снова начинаете осматривать свой нож, поглаживаете его острие. Какой он красивый... моя прелесть...
inv+ Ножик
ножек=1
if rt=1 then goto 4s4b
goto 2b1
end


:2a5
if сил<60 then pln Куда уж с вашими силами - только топиться...
if сил<60 then goto 2b1
if сил>80 then pln С трудом, но вы все же взобрались на эту горку. Теперь совсем "немного" - залезть по канату...
сил=сил-15
inv- 15,Сил
if сил<1 then goto down1
btn up1,Лезть выше
btn 2b19,Спуститься
end

:2b19
if nrt=1 then goto 4s4b
goto 2b1
end

:up1
pln Раскачиваясь, вы лезете по веревке все выше и выше, чувствуя в себе силы... Поворот головы и скала в нескольких метрах снизу. Еще есть шанс спуститься, потом путь будет только вверх.
сил=сил-10
inv- 10,Сил
if сил<1 then goto down1
btn up2,Лезть выше
btn 2b19,Спуститься
end

:down1
pln Руки изменили вам и вы рухнули на острые камни
end

:up2
сил=сил-10
inv- 10,Сил
if сил<1 then goto down1
pln Закусив губу, вы лезете все выше и выше...
btn up3,Выше...
end

:up3
сил=сил-10
inv- 10,Сил
if сил<1 then goto down1
pln Выше и выше...
goto up4
end

:up4
сил=сил-10
inv- 10,Сил
if сил<1 then goto down1
pln Вы поднялись выше площадки, откуда сбрасывали смертников, и тут вас увидела стража. Один седовласый пикейщик и парень с арбалетом стояли у бойницы и глядели на вас. Парень снял арбалет прицелился в вас...
btn up5,Вверх
btn up4a,Стоять
btn up4b,Раскочать веревку
end

:up4b
сил=сил-15
inv- 15,Сил
if сил<1 then goto down1
pln Вы начали раскачивать веревку... Занятие было бы простым если бы веревка не была такой толстой и длиной, а вы не находились в середине. Но как это не странно, стрелы не последовало. Сквозь рябь в глазах вы видели как старый воин кричит на паренька.
btn up5,Выше
end

:up4a
pln Вы спокойно глядели им в глаза не двигаясь с места. Или смелость покорила паренька или он так шутил, но во всяком случае арбалет он опустил, улыбнулся
btn up5,Выше
end

:up5
сил=сил-10
inv- 10,Сил
if сил<1 then goto down1
pln Прогоняя неприятные мысли, вы лезли и лезли вверх. Стрелы снизу так и не последовало. А сверху уже маячил конец пути. Подумать только, так близко!
btn up6,Выше
end

:up6
сил=сил-10
inv- 10,Сил
if сил<1 then goto down1
kd=kd+1
pln закусив губу, вы лезете и лезете...
if kd<3 then goto up6
pln Вы сделали это! Вы достигли конца пути. Перед вами, всего в метре, каменистая площадка за которой начинается зеленое поле. Свобода. Единственное что портит впечатление, это стражник с пикой, стоящий совсем рядом. Атаковать он, видно, не собирается. И дает понять что правила он соблюдать будет...
pln теперь надо решить: уйдете ли вы свободным сейчас или попытаетесь, преодолев усталость, долезть до конца веревки?
btn up7,Выше
btn up6a,Спрыгнуть
if Ножек=1 then btn up6b,Спрыгнуть и ударить ножом
end

:up6a
pln Вы спрыгиваете на площадку и со всех ног бежите в поле. Сзади вас раздается веселый хохот стражников...
pln
pln
pln
pln Конец
end

:up6b
pln Стражник не ожидал атаки и повалился с перерезанным горлом. Вы затравлено оглянулись и ещё успели увидеть наконечник стрелы, летящий вам в голову.
pln По крайней мере вы умерли свободным
end

:up7
сил=сил-10
inv- 10,Сил
if сил<1 then goto down1
pln вы лезете и лезете...
if ho=3 then pln осталось совсем немного
if ho=4 then goto up8
ho=ho+1
goto up7
end

:up8
сил=сил-10
inv- 10,Сил
if сил<1 then goto down1
pln Вы почти добрались. Но тут произошло пренеприятное событие: Только сейчас вы подумали, что не сможете забраться наверх. Веревка плотно прилегала к скале в этом месте из-за своего громадного веса и вы никак не могли бы обхватить веревку....
btn up6,Спуститься вниз
btn up8a,Влезть, цепляясь за скалу
if ножек=1 then btn up9,Перерезать веревку снизу
end

:up8a
pln Вы отчаянно цепляетесь за маленькие трещины в скале, но они не способны удержать вас.
goto down1
end

:up9
pln Вы ели-ели перерезаете канат снизу отчего верхняя часть становится легкой и уже спокойно залезаете наверх. Большое каменное плато было последним что вы видели - ваше тело решило за вас... вы просто повалились на камни, желая отдышаться...
pln От созерцания неба вас отвлекли многочисленные шаги. Подняв голову, вы увидели с два десятка рабов, шестеро из которых несли на своих плечах носилки с креслом для Главного Жреца, а так же несколько жрецов, чьи лица вы не раз видели на разнообразных монетах. Один из жрецов, одетый в наиболее яркие одежды, поклонился и молча указал на трон. Двое жрецов помогли подняться и подвели вас к креслу. Рабы поставили носилки...
btn up10,Сесть
btn up11,Стоять
end

:up10
pln Вы сели в кресло, а рабы подняли вас. Жрец указал что-то одному из рабов.
goto up13
end

:up11
pln Ни мало не смутившись такому поступку, жрец повел рукой и рабы подняли пустые носилки. 
goto up13
end

:up13
pln Через секунду, словно из ничего, стали появляться разнообразные кушанья. Все аккуратно подносили вам и ждали, держа в руках подносы... тут же, на спинку стула, опустились одежды Главного Жреца, обрамленные золотом. 
pln Внезапно раздался треск... Раб случайно встал на оброненную вами раковину. Теперь же он замер не двигаясь...
btn up14,Указать на него пальцем
btn up15,Есть
end

:up14
pln Вы показываете на раба своим скрюченным пальцем и жрец, кивнув, подпрыгивает к рабу, вонзая в него ниоткуда появившийся маленький стилет. Раб молча падает...
goto up15
end

:up15
pln Не обращая больше ни на что внимание, вы хватаете с ближайшего подноса какую-то еду и с жадностью пожираете её. Теперь вам можно все...
pln
pln
pln
pln Конец
end
